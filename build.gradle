buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
        classpath "com.bmuschko:gradle-nexus-plugin:2.3.1"
    }
}

description = "LDAP integration with Spring Boot"

ext.sampleModules = subprojects.findAll { project -> project.name.endsWith('plain') || project.name.endsWith('odm') }
ext.coreModules = subprojects - sampleModules

configure(coreModules) {

    apply plugin: "java"
    apply plugin: "propdeps-maven"
    apply plugin: "com.bmuschko.nexus"

    group='com.github.eddumelendez.ldap'

    sourceCompatibility=1.6
    targetCompatibility=1.6

    repositories {
        mavenCentral()
    }

    modifyPom {
        project {
            def generatedDeps = dependencies
            dependencies {
                generatedDeps.removeAll { dep ->
                    dep.scope == "optional" || dep.scope == "test"
                }
                generatedDeps.each { dep ->
                    dependency {
                        groupId dep.groupId
                        artifactId dep.artifactId
                        if (dep.version)
                            version dep.version
                        scope dep.scope
                    }
                }
            }

            name project.description
            description = project.description
            url = "https://github.com/eddumelendez/ldap-spring-boot"
            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution "repo"
                }
            }
            scm {
                url = "https://github.com/eddumelendez/ldap-spring-boot"
                connection = "scm:git:git://github.com/eddumelendez/ldap-spring-boot"
                developerConnection = "scm:git:git://github.com/eddumelendez/ldap-spring-boot"
            }
            developers {
                developer {
                    id = "eddumelendez"
                    name = "Eddú Meléndez"
                    email = "eddu.melendez@gmail.com"
                }
            }
            issueManagement {
                system = "GitHub"
                url = "https://github.com/eddumelendez/ldap-spring-boot/issues"
            }
        }
    }

}