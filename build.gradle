buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://repo.spring.io/plugins-release'
        }
    }
    dependencies {
        classpath "org.springframework.build.gradle:propdeps-plugin:0.0.7"
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
    }
}

description = "LDAP integration with Spring Boot"

ext.sampleModules = subprojects.findAll { project -> project.name.endsWith('plain') || project.name.endsWith('odm') }
ext.coreModules = allprojects - sampleModules

configure(coreModules) {

    apply plugin: "java"

    group='com.github.eddumelendez.ldap'

    sourceCompatibility=1.6
    targetCompatibility=1.6

    repositories {
        mavenCentral()
    }

    task sourcesJar(type: Jar) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
    }

    assemble.dependsOn sourcesJar, javadocJar

}
